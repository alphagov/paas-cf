resource "random_pet" "elb_cipher" {
  length = 1

  keepers = {
    default_classic_load_balancer_security_policy = var.default_classic_load_balancer_security_policy
  }
}

resource "aws_elb" "ssh_proxy" {
  name                      = "${var.env}-ssh-proxy"
  subnets                   = split(",", var.infra_subnet_ids)
  idle_timeout              = var.elb_idle_timeout
  cross_zone_load_balancing = "true"

  security_groups = [
    aws_security_group.sshproxy.id,
  ]

  health_check {
    target              = "TCP:2222"
    interval            = var.health_check_interval
    timeout             = var.health_check_timeout
    healthy_threshold   = var.health_check_healthy
    unhealthy_threshold = var.health_check_unhealthy
  }

  listener {
    instance_port     = 2222
    instance_protocol = "tcp"
    lb_port           = 2222
    lb_protocol       = "tcp"
  }

  access_logs {
    bucket = data.aws_s3_bucket.account_region_wide_alb_access_logs.id
    bucket_prefix = "${var.env}/ssh-proxy"
    enabled = true
  }
}
